buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

allprojects {
    defaultTasks 'build'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'ninja.miserable'
    version = '2.0.0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jar {
        classifier = 'original'
    }

    shadowJar {
        classifier = ''
    }

    artifacts {
        archives shadowJar
    }

    processResources {
        filter ReplaceTokens, tokens: [
            'project.version': project.version
        ]
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                if (project.hasProperty('exhaustMavenUser') && project.hasProperty('exhaustMavenPass')) {
                    repository(url: project.version.endsWith('-SNAPSHOT') ? 'http://repo.miserable.ninja/content/repositories/snapshots/' : 'http://repo.miserable.ninja/content/repositories/releases/') {
                        if (project.hasProperty('exhaustMavenUser') && project.hasProperty('exhaustMavenPass')) {
                            authentication(userName: project.exhaustMavenUser, password: project.exhaustMavenPass)
                        }
                    }
                }
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name 'miserableninja-repo'
            url 'http://repo.miserable.ninja/content/groups/public/'
        }
        maven {
            name 'sk89q-repo'
            url 'http://maven.sk89q.com/repo/'
        }
    }

    dependencies {
        compile 'com.sk89q.intake:intake:4.2-SNAPSHOT'
    }
}
